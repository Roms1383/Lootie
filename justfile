set windows-shell := ["powershell.exe", "-NoLogo", "-Command"]
set dotenv-load

# default to steam default game dir
DEFAULT_GAME_DIR := join("C:\\", "Program Files (x86)", "Steam", "steamapps", "common", "Cyberpunk 2077")

game_dir            := env_var_or_default("GAME_DIR", DEFAULT_GAME_DIR)
plugin_name         := 'lootie'

# codebase (here)
red4ext_bin_dir     := join(justfile_directory(), "target")
redscript_repo_dir  := join(justfile_directory(), "reds")

# game files
red4ext_deploy_dir    := join("red4ext", "plugins", plugin_name)
redscript_deploy_dir  := join("r6", "scripts", capitalize(plugin_name))
red_cache_dir         := join("r6", "cache")

[private]
setup path:
  @if (!(Test-Path '{{path}}')) { [void](New-Item '{{path}}' -ItemType Directory); Write-Host "Created folder at {{path}}"; }

[private]
delete path:
  @if (Test-Path '{{path}}') { [void](Remove-Item -Force -Recurse '{{path}}'); Write-Host "Deleted folder at {{path}}"; }

[private]
copy from to:
  @Copy-Item -Force '{{from}}' '{{to}}'
  @Write-Host "Copied {{from}} -> {{to}}"

[private]
copy-recurse from to:
  @Copy-Item -Force -Recurse '{{from}}' '{{to}}'
  @Write-Host "Copied {{from}} -> {{to}}"

# ğŸ•“ log current time
[private]
now:
  @Write-Host "$(Get-Date) $_"

# ğŸ“¦ build Rust RED4Ext plugin
build PROFILE='debug' TO=game_dir: (setup join(TO, red4ext_deploy_dir))
  @'{{ if PROFILE == "release" { `cargo build --release` } else { `cargo build` } }}'
  @just copy '{{ join(red4ext_bin_dir, PROFILE, plugin_name + ".dll") }}' '{{ join(TO, red4ext_deploy_dir, plugin_name + ".dll") }}'
  @just now

alias b := build

# ğŸ§‘â€ğŸ’» build & reload
dev: (build) reload

# ğŸ§ debug with CodeLLDB
lldb TO=game_dir: dev
  @just copy '{{ join(red4ext_bin_dir, "debug", plugin_name + ".pdb") }}' '{{ join(TO, red4ext_deploy_dir, plugin_name + ".pdb") }}'
  @just now

checksum IN OUT:
  Get-FileHash -Path "{{IN}}" -Algorithm SHA256 | Select-Object -ExpandProperty Hash > "{{OUT}}"

# ğŸ¤– package in CI
ci TO: (setup join(TO, red4ext_deploy_dir)) (setup join(TO, redscript_deploy_dir)) (build 'release' TO) (reload TO)

# ğŸ”¥ clear cache
clear:
    @if(Test-Path "{{ join(red_cache_dir, 'final.redscripts.bk') }}" ) { \
        Write-Host "replacing {{ join(red_cache_dir, 'final.redscripts.bk') }} with {{ join(red_cache_dir, 'final.redscripts.bk') }}"; \
        cp -Force '{{ join(red_cache_dir, "final.redscripts.bk") }}' '{{ join(red_cache_dir, "final.redscripts") }}'; \
        Remove-Item -Force -Path '{{ join(red_cache_dir, "final.redscripts.bk") }}'; \
    } else { \
        Write-Host "missing {{ join(red_cache_dir, 'final.redscripts.bk') }}"; \
    }

# ğŸ”„ overwrite Redscript file(s)
reload TO=game_dir: (setup join(TO, redscript_deploy_dir))
  @just copy-recurse '{{ join(redscript_repo_dir, "*") }}' '{{ join(TO, redscript_deploy_dir) }}'
  @just now

alias r := reload

# ğŸ—‘ï¸  uninstall mod
uninstall FROM=game_dir:
  just delete '{{ join(FROM, red4ext_deploy_dir) }}'
  just delete '{{ join(FROM, redscript_deploy_dir) }}'

# ğŸ“ format code
format:
  @cargo fmt

# ğŸ¨ lint code
@lint:
  cargo clippy --fix --allow-dirty --allow-staged
  cargo fix --allow-dirty --allow-staged
  just format

alias l := lint

# ğŸ”· quality insurance (format, lint, etc)
qa:
  @cargo clippy -- -D warnings
  @cargo fix
  @cargo fmt --check

# âœ… unit-tests
@test:
  echo 'TODO'; exit 0

alias t := test

# â˜‘ï¸  compilation check
check:
  @cargo check --all

alias c := check

# ğŸ—’ï¸  documentation
@doc:
  cargo doc --open --no-deps --document-private-items
